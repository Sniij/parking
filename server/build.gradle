plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.13'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'

	// asciidoc 파일을 변환해주고, build 폴더에 복사해주는 플러그인
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'com.sniij'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '11'
}


configurations {
	// AsciiDoctor 에서 사용되는 의존 그룹 지정.
	asciidoctorExtensions
}


repositories {
	mavenCentral()
}


ext {
	// API 문서 스니펫이 생성될 경로를 지정
	snippetsDir = file('build/generated-snippets')
}


dependencies {

	// rest docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	implementation 'org.springframework.restdocs:spring-restdocs-core:2.0.7.RELEASE'
	asciidoctorExtensions  'org.springframework.restdocs:spring-restdocs-asciidoctor'

	// TEST 라이브러리
	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	// AWS paramStore 사용 Spring boot 버전 호환 상세 : https://spring.io/projects/spring-cloud
	// S3 Client 활성화
	implementation "org.springframework.cloud:spring-cloud-starter-aws-parameter-store-config:2.2.6.RELEASE"
	implementation "io.awspring.cloud:spring-cloud-starter-aws-parameter-store-config:2.3.0"
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'


	// Entity 와 DTO 간 데이터 구조 변환을 위한 dependency
	implementation 'org.mapstruct:mapstruct:1.5.2.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'

	// Spring Web 기본 dependency
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// security
	//implementation 'org.springframework.boot:spring-boot-starter-security'
	//implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	//implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// 유효성 검증을 위한 dependency
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Spring Data JPA 사용을 위함
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// MySQL8 DB
	runtimeOnly 'mysql:mysql-connector-java:8.0.28'

	// GSON 라이브러리 : Java 객체 <=> JSON
	implementation 'com.google.code.gson:gson:2.10.1'


	// 애너테이션 프로세싱 기능 활성화
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'


	// webflux, Web client 사용하기 위함
	implementation 'org.springframework.boot:spring-boot-starter-webflux'


}


tasks.named('test') {
	// test task 실행 시 API 문서 생성 스니펫 디렉토리 경로 설정
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	// asciidoctor task 실행 시 Asciidoctor 기능을 사용하기 위한 asciidoctorExtensions configuration
	configurations "asciidoctorExtensions"
	inputs.dir snippetsDir
	dependsOn test
}

// copyDocument task 실행 시 index.html 파일이 복사
task copyDocument(type: Copy) {
	// asciidoctor task 가 실행된 후에 task 가 실행되도록 의존성 설정
	dependsOn asciidoctor
	// "build/docs/asciidoc/" 경로에 생성되는 index.html 을 copy
	from file("${asciidoctor.outputDir}")
	// "src/main/resources/static/docs" 경로로 index.html 을 추가
	into file("src/main/resources/static/docs")   // (8-3)
}

build {
	// build task 가 실행되기 전에 copyDocument task 가 먼저 수행되도록
	dependsOn copyDocument
}

// 애플리케이션 실행 파일이 생성
bootJar {
	// bootJar task 실행 전에 copyDocument task 가 실행되도록 의존성을 설정
	dependsOn copyDocument
	// Asciidoctor 실행으로 생성되는 index.html 파일을 jar 파일 안에 추가
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}
